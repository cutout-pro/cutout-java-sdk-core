plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'pro.cutout'
version = '1.1.2'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

allprojects {
    tasks.withType(JavaCompile){
        options.encoding = "UTF-8"
    }
}

jar {
    zip64 true
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifestContentCharset 'UTF-8'
    manifest {
        attributes 'Main-Class': "pro.cutout.api.UnitTests"
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

dependencies {
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
    implementation group: 'com.alibaba', name: 'fastjson', version: '2.0.9'
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'

}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourcesJar
            pom {
                name = "cutout-java-sdk-core"
                description = "cutout-java-sdk-core"
                url = "https://github.com/cutout-pro/cutout-java-sdk-core"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "CUTOUT"
                        name = "CUTOUT"
                        email = "tech@cutout.pro"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/cutout-pro/cutout-java-sdk-core"
                    developerConnection = "scm:git:https://github.com/cutout-pro/cutout-java-sdk-core"
                    url = "https://github.com/cutout-pro/cutout-java-sdk-core"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }

}


